// $THETA  value1  [value2]  [value3] ...
//          [(value)xn]
//          [NUMBERPOINTS=n]
//          [ABORT|NOABORT|NOABORTFIRST]
//
// 5 legal forms
//   1. init [FIXED]
//   2. ([low,] init [,up] [FIXED])
//   3. ([low,] init [,up]) [FIXED]  // MUST have "low" if "up" exists
//   4. (low,,up)  // has NO init
//   5. (value)xn  // value is inside parenthesis of form 2-4
//
// Rules
//   1. "low" and "up" can be skipped with -INF/INF (default).
//   2. "FIXED" requires "low" & "up" to equal "init", insofar they appear.
//   3. "FIXED" implied if "low"="init"="up".

root : blank* theta ( blank+ (theta | option) )* blank*

?option : KEY blank* "=" blank* VALUE -> option
        | VALUE                       -> option

theta : init fix?                        // form 1
      | _lpar init _rpar                 // form 2+3+5 (init)
      | _lpar low sep init _rpar         // form 2+3+5 (low, init)
      | _lpar low sep init sep up _rpar  // form 2+3+5 (low, init, up)
      | _lpar low sepsep up _rpar        // form 4+5 (low, up)
sep : blank+
    | blank* "," blank*
sepsep : blank* "," blank* "," blank*
_lpar : "(" blank*
_rpar : blank* fix? ")" n
      | blank* ")" n fix?
      | blank* ")" fix?
      | blank* fix? ")"

init : NUMERIC
low  : NUMERIC | NEG_INF
up   : NUMERIC | POS_INF
n    : "x" INT

// generic option terminals (key/value)
KEY   : ( "NUMBERPOINTS" | ( "ABORT" | "NOABORT" | "NOABORTFIRST" ) )
VALUE : INT

%import .definitions (blank, fix, INT, NUMERIC, WS)

NEG_INF: "-INF" | "-1000000"
POS_INF: "INF" | "1000000"
