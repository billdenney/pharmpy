// $OMEGA  [DIAGONAL(n)|BLOCK(n)|BLOCK(n) SAME(m)|BLOCK SAME(m)]
//         [BLOCK(n)VALUES(diag,odiag)]
//         [VARIANCE|STANDARD]  [COVARIANCE|CORRELATON] [CHOLESKY]
//         [[value1]  [value2]  [value3] ... [FIXED]]
//         [(value)xn]
//
// 4 legal forms
//   1. $OMEGA   [DIAGONAL(n)] [ v11 v22 v33 ... vnn ]
//   2. $OMEGA BLOCK(n) [ v11 v21 v22 v31 v32 v33 ... vn1 vn2 ... vnn ]
//   3. $OMEGA BLOCK(n) SAME(m)
//   4. $OMEGA BLOCK(n) VALUES(diag,odiag)

// Samples from nmhelp:
//   %input | BLOCK(3)  6. .005 .3 .0002 .006 .4|
//   %input | .04 .12|
//   %input | BLOCK(2) .04 .002 .12|
//   %input | BLOCK(2) SAME(3)|
//   %input | BLOCK(6) VALUES(0.1,0.01)|
//
// All these inputs have the same meaning:
//   %input | BLOCK(2) ; or $OMEGA VARIANCE COVARIANCE BLOCK(2)\n0.64\n-0.24 0.58|
//   %input | STANDARD BLOCK(2)\n0.8\n-0.24 0.762|
//   %input | STANDARD CORRELATION BLOCK(2)\n0.8\n-0.394 0.762|
//   %input | VARIANCE CORRELATION BLOCK(2)\n0.64\n-0.394 0.58|
//   %input | CHOLESKY BLOCK(2)\n0.8\n-0.3 0.7|

root : blank+
     | blank* [diagonal] (blank* diag_item)+ blank* // form 1
     | [_opt] block [_opt] (_item)+ blank*                          // form 2
     | [_opt] block [_opt] same blank*                              // form 3
     | [_opt] bare_block [_opt] same blank*                         // form 3 w/o n
     | [_opt] block [_opt] values (_opt | blank*)* blank*   // form 4

diag_item: init [_diag_options]
         | "(" blank* _diag_options blank* init blank* ")" [n]
         | "(" blank* init blank* _diag_options blank* ")" [n]
         | "(" blank* _diag_options blank* init blank* _diag_options blank* ")" [n]
_diag_options: (blank* (fix | SD | VAR))*

// repeat of init or comment (form 1 & 2)
_item : blank* omega | blank*

// can be tied to WHOLE record or ONE INIT
_opt     : (blank* (fix | VAR | SD | CORR | COV | CHOLESKY))* blank*  // note ws injection as 1st child
VAR      : "VARIANCE" | "VARIANC" | "VARIAN" | "VARIA" | "VARI" | "VAR" | "VA" | "V"
SD       : "STANDARD" | "STANDAR" | "STANDA" | "STAND" | "STAN" | "STA" | "ST" | "SD" | "S"
CORR     : "CORRELATION" | "CORRELATIO" | "CORRELATI" | "CORRELAT" | "CORRELA" | "CORREL" | "CORRE" | "CORR" | "COR"
COV      : "COVARIANCE" | "COVARIANC" | "COVARIAN" | "COVARIA" | "COVARI" | "COVAR" | "COVA" | "COV"
CHOLESKY : "CHOLESKY" | "CHOLESK" | "CHOLES" | "CHOLE" | "CHOL" | "CHO"

// parentheses sizes (optional for SAME and BLOCK with SAME)
block       : bare_block blank* _lpar size _rpar
bare_block  : ("BLOCK" | "BLOC" | "BLO")
same        : "SAME" [_lpar size _rpar]
diagonal    : ("DIAGONAL" | "DIAGONA" | "DIAGON" | "DIAGO" | "DIAG" | "DIA") blank* _lpar blank* size blank* _rpar blank*
values      : "VALUES" _lpar diag sep odiag _rpar

size  : INT
diag  : NUMERIC
odiag : NUMERIC

// separator and clustering of init values
sep : WS+ | WS* "," WS*
_lpar : "(" blank*
_rpar : blank* ")"

// omega init value
omega : init _opt*
      | _lpar init _opt* _rpar [n]
      | _lpar _opt* init _rpar [n]

init  : NUMERIC
n     : "x" INT

%import .definitions (blank, fix, INT, NUMERIC, WS)
